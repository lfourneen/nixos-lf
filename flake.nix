{
  description = "NixOS Configuration";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-26.05";

    nixpkgs-unstable.url = "github:NixOS/nixpkgs/nixos-unstable";

    disko = {
      url = "github:nix-community/disko";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    impermanence = {
      url = "github:nix-community/impermanence";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    home-manager = {
      url = "github:nix-community/home-manager/release-26.05";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    sops-nix = {
      url = "github:Mic92/sops-nix";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    mcp-nixos = {
      url = "github:utensils/mcp-nixos";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    noctalia.url = "github:noctalia-dev/noctalia/cachix";
    
    nix-flatpak.url = "github:gmodena/nix-flatpak";

    hermes-agent.url = "github:NousResearch/hermes-agent/v0.21.6";

    llm-agents.url = "github:numtide/llm-agents.nix";
  };

  outputs = { self, nixpkgs, home-manager, ... }@inputs:

  let
    system = "x86_64-linux";

  in 
  {
    nixosConfigurations.lfour = nixpkgs.lib.nixosSystem {
      inherit system;

      specialArgs = { inherit inputs; };

      modules = [
        # System config
        ./system

        # Proper nixpkgs configuration module
        {
          nixpkgs = {
            overlays = [
              # Custom overlays
              (import ./overlays)

              # MCP NixOS overlay
              (final: prev: {
                mcp-nixos = inputs.mcp-nixos.packages.${prev.stdenv.hostPlatform.system}.default;
              })

              # LLM agents overlay
              (final: prev: {
                dsh = inputs.llm-agents.packages.${prev.stdenv.hostPlatform.system}.dsh;
                oh-my-codex = inputs.llm-agents.packages.${prev.stdenv.hostPlatform.system}.oh-my-codex;
                oh-my-opencode = inputs.llm-agents.packages.${prev.stdenv.hostPlatform.system}.oh-my-opencode;
              })

              # Shared nixpkgs-unstable as pkgs.unstable.*
              (final: prev: {
                unstable = import inputs.nixpkgs-unstable {
                  system = prev.stdenv.hostPlatform.system;
                  config = prev.config // {
                    allowUnfree = true;
                  };
                };
              })
            ];
            config.allowUnfree = true;
          };
        }

        # Home Manager Integration
        home-manager.nixosModules.home-manager
        {
          home-manager = {
            useGlobalPkgs = true;
            useUserPackages = true;
            backupFileExtension = "backup";

            extraSpecialArgs = {
              inherit inputs;

            };
            users.lfour = {
              imports = [ ./home ];
            };
          };
        }

        # File System
        inputs.disko.nixosModules.default
        inputs.impermanence.nixosModules.impermanence
        
        # Sops(secrets managemnet)
        inputs.sops-nix.nixosModules.sops

        # Flatpak
        inputs.nix-flatpak.nixosModules.nix-flatpak

        # Hermes Agent
        inputs.hermes-agent.nixosModules.default
      ];
    };
  };
}

